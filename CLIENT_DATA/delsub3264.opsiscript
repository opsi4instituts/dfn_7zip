; Parameter: $SearchPattern$: Suchbegriff in Registry
Set $SearchPattern$ = $ProductName$
Sub_search_registry3264_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

if (count ($ResultList$) = "0")
	comment "No installations of " + $SearchPattern$ + " found in registry"
else
	comment "Found at least one installation of " + $SearchPattern$ + " in registry, try to deinstall"
	Message "Uninstalling " + $ProductName$ + " ..."
	for %s% in $ResultList$ do Sub_uninstall

	comment "Delete common desktop icon, if any"
	if $DesktopLink$ = "True"
        LinkFolder_delete_desktoplink
    endif	

	comment "include custom post deinstall file"
	set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostDeinstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
		endif
	endif	
endif

[Sub_uninstall]
Set $RegId$ = "%s%"

; Patch um auch vorhandene Installation durch Exe-Installer zu deinstallieren (Danke an Martin Niebergall) 
; we don't know at this point if RegId is 64 or 32 bit
; try 64bit first
Set $UninstallProgram$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] UninstallString")
if contains(lower($UninstallProgram$), "msiexec")
        Winbatch_uninstall_msi
        sub_check_exitcode
else    
        if contains(lower($UninstallProgram$), "uninstall.exe")
                Winbatch_uninstall_exe
                ; TODO: check exit codes? same as MSI?
        endif
endif

; retry the same for 32bit version
Set $UninstallProgram$ = GetRegistryStringValue32("[" + $RegPathUninstall$ + "\" + $RegId$ + "] UninstallString")
if contains(lower($UninstallProgram$), "msiexec")
        Winbatch_uninstall_msi
        sub_check_exitcode
else    
        if contains(lower($UninstallProgram$), "uninstall.exe")
                Winbatch_uninstall_exe
                ; TODO: check exit codes
        endif
endif


[Winbatch_uninstall_msi]
msiexec /x $RegId$ $SilentOption$ REBOOT=ReallySuppress

[Winbatch_uninstall_exe]
"$UninstallProgram$" /S

[Linkfolder_delete_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$
