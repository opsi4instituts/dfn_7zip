; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

include_append "%ScriptPath%\check_msi_exitcode.opsiscript"

DefVar $MsiId$
DefVar $displayName32$
DefVar $displayName64$
DefStringList $MsiList$

Set $MsiId$ = '{23170F69-40C1-2701-0938-000001000000}' 

;7-Zip 9.20
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2701-0920-000001000000}')

;7-Zip 9.20 (x64 edition)
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2702-0920-000001000000}')

;7-Zip 9.20 (ia64 edition)
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2703-0920-000001000000}')

;7-Zip 9.38
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2701-0938-000001000000}')

;7-Zip 9.38 (x64 edition)
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2702-0938-000001000000}')

;7-Zip 15.12
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2701-1512-000001000000}')

;7-Zip 15.12 (x64 edition)
Set $MsiList$ = addtolist($MsiList$, '{23170F69-40C1-2702-1512-000001000000}')

for %actmsi% in $msilist$ do sub_uninstallmsi

[sub_uninstallmsi]
Set $MsiId$ = "%actmsi%"
Set $displayName32$ = GetRegistryStringValue32("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $MsiId$ + "] DisplayName")
Set $displayName64$ = GetRegistryStringValue64("[HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\" + $MsiId$ + "] DisplayName")
if not ($displayName32$ = "") or not ($displayName64$ = "")
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$
  Message "Uninstalling " + $PrettyName$ + " ..."
	comment "MSI id " + $MsiId$ + " found in registry, starting msiexec to uninstall"
	Winbatch_uninstall_msi
	;Winbatch_uninstall
	sub_check_exitcode
endif

;-----------------------------------------------------
[Winbatch_uninstall_msi]
msiexec /x $MsiId$ /qb! REBOOT=ReallySuppress

[Sub_check_exitcode]
comment "Test for installation success via exit code"
set $ExitCode$ = getLastExitCode
; informations to exit codes see
; http://msdn.microsoft.com/en-us/library/aa372835(VS.85).aspx
; http://msdn.microsoft.com/en-us/library/aa368542.aspx
if ($ExitCode$ = "0")
	comment "Looks good: setup program gives exitcode zero"
else
	comment "Setup program gives a exitcode unequal zero: " + $ExitCode$
	if ($ExitCode$ = "1605")
		comment "ERROR_UNKNOWN_PRODUCT	1605	This action is only valid for products that are currently installed."
		comment "Uninstall of a not installed product failed - no problem"
	else
		if ($ExitCode$ = "1641")
			comment "looks good: setup program gives exitcode 1641"
			comment "ERROR_SUCCESS_REBOOT_INITIATED	1641	The installer has initiated a restart. This message is indicative of a success."
		else
			if ($ExitCode$ = "3010")
				comment "looks good: setup program gives exitcode 3010"
				comment "ERROR_SUCCESS_REBOOT_REQUIRED	3010	A restart is required to complete the install. This message is indicative of a success."
			else
				logError "Fatal: Setup program gives an unknown exitcode unequal zero: " + $ExitCode$
				isFatalError
			endif
		endif
	endif
endif

