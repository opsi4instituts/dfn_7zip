; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

; Parameter: $SearchPattern$: Suchbegriff in Registry
Set $SearchPattern$ = $PrettyName$
Sub_search_registry3264_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

if (count ($ResultList$) = "0")
	comment "No installations of " + $SearchPattern$ + " found in registry"
else
	comment "Found at least one installation of " + $SearchPattern$ + " in registry, try to deinstall"
	Message "Uninstalling " + $PrettyName$ + " ..."
	for %s% in $ResultList$ do Sub_uninstall

	comment "Delete common desktop icon, if any"
	Linkfolder_uninstall_desktopicon	

	comment "include custom post deinstall file"
	set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostDeinstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
		endif
	endif	
endif

[Sub_uninstall]
Set $RegId$ = "%s%"
Winbatch_uninstall_msi
sub_check_exitcode

[Winbatch_uninstall_msi]
msiexec /x $RegId$ /qn REBOOT=ReallySuppress

[Linkfolder_uninstall_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $PrettyName$
