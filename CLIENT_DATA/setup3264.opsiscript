; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/
 
[Actions]
requiredWinstVersion >= "4.11.4.6"

; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	comment "Show product picture"
	ShowBitmap "%ScriptPath%\o4i.png" $PrettyName$ 

	if FileExists("%ScriptPath%\delsub3264.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub3264.opsiscript"
	endif
	
	comment "installing"

	if (($INST_SystemType$ = "x86 System") and ($INST_architecture$ = "system specific")) or ($INST_architecture$ = "32 only")
		Message "Installing %installingProdName% (%installingProdVersion%) 32 Bit..."
		comment "Start setup program"
		Set $InstFile$ = $InstFile32$
		Winbatch_install
		Sub_check_exitcode
	endif

	if ($INST_SystemType$ = "64 Bit System") and (($INST_architecture$ = "system specific") or ($INST_architecture$ = "64 only"))
		Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."
		comment "Start setup program"
		Set $InstFile$ = $InstFile64$		
		Winbatch_install
		Sub_check_exitcode
	endif

	Set $InstallDir$ = GetRegistryStringValueSysnative("[HKEY_LOCAL_MACHINE\Software\" + $PrettyName$ + "] Path")
	Set $PathToExe$ = $InstallDir$ + "\" + $Exe$
	
	Set $DesktopIcon$ = getProductProperty("desktopicon","no")
	if $DesktopIcon$ = "yes"
		comment "Create common desktop icon"
		Linkfolder_install_desktopicon	
	endif	

	comment "include custom post install file"
	Set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
	if not ($CustomPostInstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
		endif
	endif
endif

[Winbatch_install]
msiexec /qn /i "%ScriptPath%\$InstFile$" /l* "$LogDir$\$ProductId$.install_log.txt" ALLUSERS=1 REBOOT=ReallySuppress

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_link
 	name: $PrettyName$
 	target: $PathToExe$
end_link

